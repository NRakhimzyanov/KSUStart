#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПроизводствоПродукцииСписокТоваров.Товар КАК Номенклатура,
		|	СУММА(ПроизводствоПродукцииСписокТоваров.Количество) КАК Количество,
		|	ПроизводствоПродукцииСписокТоваров.Ссылка.Дата КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
		|ИЗ
		|	Документ.ПроизводствоПродукции.СписокТоваров КАК ПроизводствоПродукцииСписокТоваров
		|ГДЕ
		|	ПроизводствоПродукцииСписокТоваров.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПроизводствоПродукцииСписокТоваров.Товар,
		|	ПроизводствоПродукцииСписокТоваров.Ссылка.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроизводствоПродукцииСписокМатериалов.Материал,
		|	СУММА(ПроизводствоПродукцииСписокМатериалов.Количество),
		|	ПроизводствоПродукцииСписокМатериалов.Ссылка.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|ИЗ
		|	Документ.ПроизводствоПродукции.СписокМатериалов КАК ПроизводствоПродукцииСписокМатериалов
		|ГДЕ
		|	ПроизводствоПродукцииСписокМатериалов.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПроизводствоПродукцииСписокМатериалов.Материал,
		|	ПроизводствоПродукцииСписокМатериалов.Ссылка.Дата,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидДвижения";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
		Если РезультатЗапроса.Пустой() Тогда
			
			Возврат;
			
		КонецЕсли;
	
	ВыборкаДетальныеЗаписи 	= РезультатЗапроса.Выбрать();
	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Движение = Движения.ОстаткиНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);	
				
		КонецЦикла;
	
	Движения.ОстаткиНоменклатуры.Записать();
	
	ОстатокНоменклатуры = РегистрыНакопления.ОстаткиНоменклатуры.
			ПроверитьОтрицательныеОстаткиМатериала(Ссылка, Дата);
	
		Если ОстатокНоменклатуры Тогда
					
			Отказ = Истина;
			
		КонецЕсли;
		
КонецПроцедуры

#КонецЕсли


#КонецОбласти