
#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетнаяВедомостьСписокСотрудников.Ссылка.Дата,
		|	РасчетнаяВедомостьСписокСотрудников.Сотрудник,
		|	РасчетнаяВедомостьСписокСотрудников.ЧасоваяСтавка КАК ЧасоваяСтавка
		|ИЗ
		|	Документ.РасчетнаяВедомость.СписокСотрудников КАК РасчетнаяВедомостьСписокСотрудников
		|ГДЕ
		|	РасчетнаяВедомостьСписокСотрудников.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;			
		КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Движения.Зарплата.Записывать = Истина;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Движение 			= Движения.Зарплата.Добавить();
		Движение.Сторно 	= Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВидыРасчета.Работа;
		Движение.ПериодРегистрации 		= ВыборкаДетальныеЗаписи.Дата;
		Движение.ПериодДействияНачало 	= НачалоМесяца(ВыборкаДетальныеЗаписи.Дата);
		Движение.ПериодДействияКонец 	= КонецМесяца(ВыборкаДетальныеЗаписи.Дата);
		Движение.Сотрудник 				= ВыборкаДетальныеЗаписи.Сотрудник;
		Движение.ЧасоваяСтавка 			= ВыборкаДетальныеЗаписи.ЧасоваяСтавка;
		
	КонецЦикла;

	Движения.Зарплата.Записать();
	Движения.Зарплата.Рассчитать();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		
    	Возврат;
    	
	КонецЕсли;
		
	Для Каждого СтрокаСотрудника Из СписокСотрудников Цикл
		
		СтрокаСотрудника.ЧасоваяСтавка = РегистрыСведений.ЧасоваяСтавкаСотрудника.ПолучитьЧасовуюСтавку
										(СтрокаСотрудника.Сотрудник, Дата);
										
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти
