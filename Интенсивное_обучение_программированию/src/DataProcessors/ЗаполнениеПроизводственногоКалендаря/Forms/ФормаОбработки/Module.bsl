
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСписокДней(Команда)
	
	Если Объект.ДатаНачала = '0001-01-01' ИЛИ Объект.ДатаОкончания = '0001-01-01' Тогда
		
		Сообщение 		= Новый СообщениеПользователю();
		Сообщение.Текст = "Интервал загрузки не указан!";
		Сообщение.Сообщить();
		ТекущийЭлемент = Элементы.ДатаНачала;
		Возврат;
		
	КонецЕсли;
	
	Объект.СписокДней.Очистить();
	ТекущийДень = Объект.ДатаНачала;
	
	Пока ТекущийДень <= Объект.ДатаОкончания Цикл
		
		НоваяСтрока = Объект.СписокДней.Добавить();
		НоваяСтрока.Дата = ТекущийДень;
		НоваяСтрока.Рабочий = (ДеньНедели(НоваяСтрока.Дата) < 6);
		НоваяСтрока.КоличествоЧасов = Объект.НормаЧасов;
		ТекущийДень = ТекущийДень + 86400;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеКалендаря(Команда)

	Если Объект.СписокДней.Количество() = 0 Тогда
		
		Сообщение 		= Новый СообщениеПользователю();
		Сообщение.Текст = "Нет данных для импорта!";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	ЗагрузитьДанныеКалендаряНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьДанныеКалендаряНаСервере()
	
	Отбор = Новый Структура ("Рабочий", Истина);
	ТаблицаГрафика = Объект.СписокДней.НайтиСтроки(Отбор);
	
	РегистрыСведений.ПроизводственныйКалендарь.ЗаполнитьГрафикПоТаблице (ТаблицаГрафика);
	
КонецПроцедуры

#КонецОбласти
